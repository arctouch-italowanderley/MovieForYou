/*
 * Copyright (c) 2017 ArcTouch LLC.
 * All rights reserved.
 *
 * This file, its contents, concepts, methods, behavior, and operation
 * (collectively the "Software") are protected by trade secret, patent,
 * and copyright laws. The use of the Software is governed by a license
 * agreement. Disclosure of the Software to third parties, in any form,
 * in whole or in part, is expressly prohibited except as authorized by
 * the license agreement.
 */

apply plugin: "com.android.application"
apply plugin: 'android-apt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.arctouch.wanderley.italo.movieforyou"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file("movieforyou.properties")))
            android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
            android.signingConfigs.release.storePassword props.keyStorePassword
            android.signingConfigs.release.keyAlias props.keyAlias
            android.signingConfigs.release.keyPassword props.keyAliasPassword
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
            // Individual variables by buildType
            resValue "string", "app_name", "MovieForYou Dev"
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
            // Individual variables by buildType
            resValue "string", "app_name", "MovieForYou"
        }
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    sourceSets {
        String sharedJavaTestDir = 'src/sharedTest/java'
        String sharedAssetsTestDir = 'src/sharedTest/assets'

        test {
            java.srcDirs += sharedJavaTestDir
            assets.srcDirs += sharedAssetsTestDir
            resources.srcDirs += sharedAssetsTestDir
        }

        androidTest {
            java.srcDirs += sharedJavaTestDir
            assets.srcDirs += sharedAssetsTestDir
        }
    }
    buildToolsVersion '25.0.0'
}

apt {
    arguments {
        resourcePackageName android.defaultConfig.applicationId
        androidManifestFile variant.outputs[0]?.processResources?.manifestFile
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android APIs
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:percent:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-annotations:$rootProject.supportLibraryVersion"

    // Dependencies for local unit tests
    testCompile "junit:junit:$rootProject.ext.junitVersion"

    // Android Annotations
    apt "org.androidannotations:androidannotations:$rootProject.ext.androidAnnotations"
    compile "org.androidannotations:androidannotations-api:$rootProject.ext.androidAnnotations"

    // Espresso UI Testing
    androidTestCompile "com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$rootProject.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$rootProject.espressoVersion"

    // Google APIs
    compile "com.google.code.gson:gson:$rootProject.ext.googleGson"
    compile "com.google.guava:guava:$rootProject.ext.guava"

    // Square
    compile "com.squareup.picasso:picasso:$rootProject.ext.picasso"
    compile "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofit"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofit"
    compile "com.squareup.okhttp3:okhttp:$rootProject.ext.okhttp"
    compile "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:$rootProject.ext.retrofitRxjavaAdapter"

    // RxJava
    compile "io.reactivex.rxjava2:rxjava:$rootProject.ext.rxjava"
    compile "io.reactivex.rxjava2:rxandroid:$rootProject.ext.rxandroid"

    // Resolve conflicts between main and test APK:
    androidTestCompile "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:design:$rootProject.supportLibraryVersion"
}
